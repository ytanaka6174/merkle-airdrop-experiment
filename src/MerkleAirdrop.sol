//SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

import {ToasterToken} from "src/ToasterToken.sol";
import {IERC20, SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {MerkleProof} from "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import {EIP712} from "@openzeppelin/contracts/utils/cryptography/EIP712.sol";
import {ECDSA} from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";


contract MerkleAirdrop is EIP712 {
    using SafeERC20 for IERC20;

    error MerkleAirdrop__InvalidProof();
    error MerkleAirdrop__AlreadyClaimed();
    error MerkleAirdrop__InvalidSignature();

    bytes32 private immutable i_merkleRoot;
    IERC20 private immutable i_token;
    mapping(address claimer => bool claimed) s_has_claimed;
    bytes32 private constant MESSAGE_TYPEHASH = keccak256("AirdropClaim(address account,uint256 amount)");

    struct AirdropClaim {
        address account;
        uint256 amount;
    }

    event ValidClaim(address indexed account, uint256 indexed amount);

    constructor(bytes32 merkleRoot, IERC20 token) EIP712("MerkleAirdrop", "1"){
        i_merkleRoot = merkleRoot;
        i_token = token;
    }

    function claim(address account, uint256 amount, bytes32[] calldata merkleProof, uint8 v, bytes32 r, bytes32 s) external {
        if (s_has_claimed[account]) {
            revert MerkleAirdrop__AlreadyClaimed();
        }
        //Check signature
        if(!_isValidSignature(account, _getMessage(account, amount), v, r, s)) {
            revert MerkleAirdrop__InvalidSignature();
        }
        bytes32 leaf = keccak256(bytes.concat(keccak256(abi.encode(account, amount))));
        if (!MerkleProof.verify(merkleProof, i_merkleRoot, leaf)) {
            revert MerkleAirdrop__InvalidProof();
        }
        s_has_claimed[account] = true; //setting before
        emit ValidClaim(account, amount);
        i_token.safeTransfer(account, amount);
    }

    function _getMessage(address account, uint256 amount) public view returns (bytes32 digest) {
        return _hashTypedDataV4(
            keccak256(abi.encode(MESSAGE_TYPEHASH, AirdropClaim({account: account, amount: amount})))
        );
    }

    function _isValidSignature(address account, bytes32 digest, uint8 v, bytes32 r, bytes32 s) internal pure returns (bool) {
        (address actualSigner, ,) = ECDSA.tryRecover(digest, v,r,s);
        return actualSigner == account;
    }

    /*//////////////////////////////////////////////////////////////
                                GETTERS
    //////////////////////////////////////////////////////////////*/

    function getHasClaimed(address claimer) external view returns (bool) {
        return s_has_claimed[claimer];
    }

    function getMerkleRoot() external view returns (bytes32) {
        return i_merkleRoot;
    }

    function getToken() external view returns (IERC20) {
        return i_token;
    }
}
